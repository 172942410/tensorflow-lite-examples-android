def targetModelFile = "src/main/assets/smartreply.tflite"
def modelDownloadUrl = "https://storage.googleapis.com/download.tensorflow.org/models/tflite/smartreply/smartreply.tflite"
def targetAARlFile = "$projectDir/libs/smartreply_runtime_aar.aar"
def aarDownloadUrl = "https://storage.googleapis.com/download.tensorflow.org/models/tflite/smartreply/smartreply_runtime_aar.aar"

class DownloadUrlTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

// Required to download TF Lite model for Smart Reply.
task downloadLiteModel(type: DownloadUrlTask) {
    doFirst {
        println "Downloading TFLite Model: ${modelDownloadUrl}"
    }

    sourceUrl = modelDownloadUrl
    target = file(targetModelFile)
}

// Required to download Prebuilt AAR Package for custom ops.
task downloadAAR(type: DownloadUrlTask) {
    doFirst {
        println "Downloading Prebuilt AAR: ${aarDownloadUrl}"
    }

    sourceUrl = aarDownloadUrl
    target = file(targetAARlFile)
}

preBuild.dependsOn downloadLiteModel
preBuild.dependsOn downloadAAR
