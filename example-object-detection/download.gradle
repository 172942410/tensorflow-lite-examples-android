apply from:'../buildscripts/network.gradle'

def targetFolder = "src/main/assets"
def modelDownloadUrl = "http://storage.googleapis.com/download.tensorflow.org/models/tflite/coco_ssd_mobilenet_v1_1.0_quant_2018_06_29.zip"
def localCache = "build/intermediates/coco_ssd_mobilenet_v1_1.0_quant_2018_06_29.zip"

task downloadModel(type: DownloadUrlTask) {
    sourceUrl = "${modelDownloadUrl}"
    target = file("${localCache}")
}

task unzipModel(type: Copy, dependsOn: 'downloadModel') {
    doFirst {
        println "Unzipping ${localCache}"
    }
    from zipTree("${localCache}")
    into "${targetFolder}"
}

task cleanUnusedFiles(type: Delete, dependsOn: ['unzipModel']) {
    delete fileTree("${targetFolder}").matching {
        include "*.pb"
        include "*.ckpt.*"
        include "*.pbtxt*"
        include "*.quant_info.*"
        include "*.meta"
    }
}

preBuild.dependsOn unzipModel
preBuild.dependsOn cleanUnusedFiles
